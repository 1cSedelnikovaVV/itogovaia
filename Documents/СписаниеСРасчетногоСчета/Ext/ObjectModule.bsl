Процедура ОбработкаПроведения(Отказ, РежимПроведения)  
	
	//проверить соответствует ли валюта счета валюте, указанной в документе-основании
	Если  СчетСписания.ВалютаСчета <> Валюта Тогда
		Сообщить("Валюта счёта не соответствует валюте, указанной в документе-основании. 
					| Выберите счёт списания для валюты " + Валюта);
	
		Отказ = Истина;	
	КонецЕсли;
	
		
	//Узнать курс указанной в документе валюты. Вызов общего модуля
	КурсВалюты = РасчетныеПроцедурыСервер.ОпределитьКурс(Дата, Валюта);  	
	Если КурсВалюты = 0 Тогда		
		Сообщить("Курс " + Валюта + " не задан!"); 
		//отказаться от проведения документа и выйти из процедуры
		Отказ = Истина;
		Возврат;			
	КонецЕсли;
	
	Движения.Хозрасчетный.Записывать = Истина; 
	
	// регистр Хозрасчетный   		
	Движение = Движения.Хозрасчетный.Добавить();
	Движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
	Движение.Период = Дата;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Получатель;    
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Договор;
	Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СчетаОрганизации] = СчетСписания;

	Если Валюта <> Справочники.Валюты.Рубль Тогда			
		
		//если счет в валюте и задан курс валюты
		//Дт60.01 - Кт52 
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.ВалютныеСчета;
		Движение.ВалютаКт = Валюта;
		Движение.Сумма = Сумма * КурсВалюты;
		Движение.ВалютнаяСуммаКт = Сумма; 
	Иначе
		// если Валюта = Рубль   
		//Дт60.01 - Кт51
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетныеСчета;
		Движение.Сумма = Сумма;
		
	КонецЕсли;   
	
КонецПроцедуры  


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		// Заполнение шапки
		//СчетСписания = 0;
		Валюта = ДанныеЗаполнения.Валюта;
		Сумма = ДанныеЗаполнения.ИтоговаяСумма;
		Получатель = ДанныеЗаполнения.Поставщик;    
		Договор = ДанныеЗаполнения.Договор;
		Основание = ДанныеЗаполнения.Ссылка;
	КонецЕсли;  
	
КонецПроцедуры
