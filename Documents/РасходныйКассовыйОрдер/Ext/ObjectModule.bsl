
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		// Заполнение шапки
		Валюта = ДанныеЗаполнения.Валюта;
		Сумма = ДанныеЗаполнения.ИтоговаяСумма;
		Поставщик = ДанныеЗаполнения.Поставщик;
		Договор = ДанныеЗаполнения.Договор;
		Основание = ДанныеЗаполнения.Ссылка;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)    
	
	//Узнать курс указанной в документе валюты. Вызов общего модуля
	КурсВалюты = РасчетныеПроцедурыСервер.ОпределитьКурс(Дата, Валюта);  	
	Если КурсВалюты = 0 Тогда		
		Сообщить("Курс " + Валюта + " не задан!"); 
		//отказаться от проведения документа и выйти из процедуры
		Отказ = Истина;
		Возврат;			
	КонецЕсли;
	
	Движения.Хозрасчетный.Записывать = Истина; 
	
	// регистр Хозрасчетный   
	Движение = Движения.Хозрасчетный.Добавить();
	Движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыПоАвансамВыданным;  
	Движение.Период = Дата;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Поставщик;    
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Договор;  
	
	Если Валюта <> Справочники.Валюты.Рубль Тогда			
		
		//если счет в валюте и задан курс валюты
		//Дт60.02 - Кт50.02 Выдача наличных из кассы
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.КассаОрганизацииВал;
		Движение.ВалютаКт = Валюта;
		Движение.Сумма = Сумма * КурсВалюты;
		Движение.ВалютнаяСуммаКт = Сумма;
	Иначе
		// если Валюта = Рубль   
		//Дт60.02 - Кт50.01
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.КассаОрганизации;
		Движение.Сумма = Сумма; 
		
	КонецЕсли;   

КонецПроцедуры
